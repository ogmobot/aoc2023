// Python regex library
from native reference re;

class Bag()
{
    function __init__(line)
    {
        reds   = re.findall("(\\d+) red",   line);
        greens = re.findall("(\\d+) green", line);
        blues  = re.findall("(\\d+) blue",  line);
        this.r = max(int(r) for r in reds);
        this.g = max(int(g) for g in greens);
        this.b = max(int(b) for b in blues);
    }

    function power()
    {
        return this.r * this.g * this.b;
    }

    function possible()
    {
        return this.r <= 12 and this.g <= 13 and this.b <= 14;
    }
}

class Main()
{
    function Main()
    {
        with open("input02.txt", "r") as fp
        {
            bags = [Bag(line) for line in fp];
        }
        print(sum(i + 1 for i, bag in enumerate(bags) if bag.possible()));
        print(sum(bag.power() for bag in bags));
    }
}
